apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdk rootProject.ext.compileSdkVersion

    namespace 'com.ianhanniballake.contractiontimer'

    defaultConfig {
        applicationId "com.ianhanniballake.contractiontimer"

        def versionMajor = 2
        def versionMinor = 4
        def versionPatch = 0
        def versionBuild = 0
        def betaVersion = 0

        minSdkVersion 21
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled true
        versionCode versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + versionBuild
        versionName "${versionMajor}.${versionMinor}" +
                (versionPatch > 0 ? ".${versionPatch}" : "") +
                (betaVersion > 0 ? ", Beta ${betaVersion}" : "")

        def filesAuthorityValue = "com.ianhanniballake.contractiontimer.files"
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
        buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""

        buildFeatures {
            compose true
        }

        composeOptions {
            kotlinCompilerExtensionVersion = "1.4.6"
        }

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
            arg("room.generateKotlin", "true")
        }
    }

    signingConfigs {
        release {
            storeFile System.getenv("KEYSTORE") != null ? file(System.getenv("KEYSTORE")) : null
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("CONTRACTION_TIMER_ALIAS")
            keyPassword System.getenv("CONTRACTION_TIMER_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.room:room-runtime:2.6.0-alpha01'
    implementation 'androidx.room:room-ktx:2.6.0-alpha01'
    ksp 'androidx.room:room-compiler:2.6.0-alpha01'
    implementation "com.google.firebase:firebase-analytics-ktx:21.2.2"
    implementation "com.google.firebase:firebase-crashlytics-ktx:18.3.7"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1"
    def composeBom = platform('androidx.compose:compose-bom:2023.05.01')
    implementation composeBom
    androidTestImplementation composeBom
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    implementation("com.opencsv:opencsv:3.8") {
        exclude group: 'commons-beanutils', module: 'commons-beanutils'
    }
    implementation "com.android.billingclient:billing-ktx:4.0.0"
}

apply plugin: 'com.google.gms.google-services'