apply plugin: 'com.android.application'

repositories {
    jcenter()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.ianhanniballake.contractiontimer"

        def versionMajor = 2
        def versionMinor = 1
        def versionPatch = 1
        def versionBuild = 0
        def betaVersion = 0

        minSdkVersion 9
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + versionBuild
        versionName "${versionMajor}.${versionMinor}" +
                (versionPatch > 0 ? ".${versionPatch}" : "") +
                (betaVersion > 0 ? ", Beta ${betaVersion}" : "")

        def filesAuthorityValue = "com.ianhanniballake.contractiontimer.files"
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
        buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""
    }

    signingConfigs {
        release {
            storeFile System.getenv("KEYSTORE") != null ? file(System.getenv("KEYSTORE")) : null
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("CONTRACTION_TIMER_ALIAS")
            keyPassword System.getenv("CONTRACTION_TIMER_PASSWORD")
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.android.support:support-annotations:23.4.0'
    compile "com.android.support:appcompat-v7:23.4.0"
    compile "com.android.support:preference-v7:23.4.0"
    compile "com.google.android.gms:play-services-drive:9.0.2"
    compile "com.google.firebase:firebase-crash:9.0.0"
    compile "com.opencsv:opencsv:3.7"
    compile project(':provider')
}

apply plugin: 'com.google.gms.google-services'