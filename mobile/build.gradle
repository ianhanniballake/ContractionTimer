apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.ianhanniballake.contractiontimer"

        def versionMajor = 2
        def versionMinor = 3
        def versionPatch = 0
        def versionBuild = 0
        def betaVersion = 0

        minSdkVersion 9
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + versionBuild
        versionName "${versionMajor}.${versionMinor}" +
                (versionPatch > 0 ? ".${versionPatch}" : "") +
                (betaVersion > 0 ? ", Beta ${betaVersion}" : "")

        def filesAuthorityValue = "com.ianhanniballake.contractiontimer.files"
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
        buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""
    }

    signingConfigs {
        release {
            storeFile System.getenv("KEYSTORE") != null ? file(System.getenv("KEYSTORE")) : null
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("CONTRACTION_TIMER_ALIAS")
            keyPassword System.getenv("CONTRACTION_TIMER_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:25.4.0"
    implementation "com.android.support:design:25.4.0"
    implementation "com.android.support:preference-v7:25.4.0"
    implementation "com.android.support:preference-v14:25.4.0"
    implementation "com.google.android.gms:play-services-base:10.0.1"
    implementation "com.google.firebase:firebase-core:10.0.1"
    implementation "com.google.firebase:firebase-crash:10.0.1"
    implementation("com.opencsv:opencsv:3.8") {
        exclude group: 'commons-beanutils', module: 'commons-beanutils'
    }
    implementation "com.android.billingclient:billing:1.1"
}

apply plugin: 'com.google.gms.google-services'