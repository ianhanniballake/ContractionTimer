apply plugin: 'com.android.application'

repositories {
    jcenter()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        def versionMajor = 2
        def versionMinor = 1
        def versionPatch = 1
        def versionBuild = 0
        def betaVersion = 0

        minSdkVersion 9
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + versionBuild
        versionName "${versionMajor}.${versionMinor}" +
                (versionPatch > 0 ? ".${versionPatch}" : "") +
                (betaVersion > 0 ? ", Beta ${betaVersion}" : "")

        def filesAuthorityValue = "com.ianhanniballake.contractiontimer.files"
        manifestPlaceholders = [filesAuthority: filesAuthorityValue]
        buildConfigField "String", "FILES_AUTHORITY", "\"${filesAuthorityValue}\""
    }

    signingConfigs {
        release {
            storeFile System.getenv("KEYSTORE") != null ? file(System.getenv("KEYSTORE")) : null
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("CONTRACTION_TIMER_ALIAS")
            keyPassword System.getenv("CONTRACTION_TIMER_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.android.support:support-annotations:23.4.0'
    compile "com.android.support:appcompat-v7:23.4.0"
    compile "com.android.support:preference-v7:23.4.0"
    compile "com.google.android.gms:play-services-drive:7.3.0"
    compile "com.google.android.gms:play-services-analytics:7.3.0"
    compile "ch.acra:acra:4.5.0"
    compile "net.sf.opencsv:opencsv:2.3"
    compile project(':provider')
}